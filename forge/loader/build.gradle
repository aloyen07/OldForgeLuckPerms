buildscript {
    dependencies {
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

plugins {
    alias(libs.plugins.shadow)
    alias(libs.plugins.forgegradle)
    id("java-library")
    id 'org.spongepowered.mixin' version '0.7.+'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

mixin {
    add sourceSets.main, 'luckperms.refmap.json'
    config 'luckperms.mixins.json'
}

minecraft {
    mappings channel: 'official', version: minecraftVersion
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraftVersion}-${forgeVersion}"
    implementation project(':api')
    implementation project(':common:loader-utils')
    implementation project(':forge:forge-api')

    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

build {
    dependsOn(":forge:build")
    dependsOn(":forge:forge-api:build")
}

jar {
    manifest {
        attributes(
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'Implementation-Title': 'LuckPerms',
                'Implementation-Vendor': 'LuckPerms',
                'Implementation-Version': project.ext.fullVersion,
                'Specification-Title': 'luckperms',
                'Specification-Vendor': 'LuckPerms',
                'Specification-Version': '1',
                "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
                "MixinConfigs": "creativerebornserver.mixins.json",
                "FMLCorePluginContainsFMLMod": "true",
                "ForceLoadAsMod": "true",
        )
    }
}

processResources {
    filesMatching('META-INF/mods.toml') {
        expand 'version': project.ext.fullVersion
    }
}

reobf {
    shadowJar {}
}

shadowJar {
    archiveFileName = "LuckPerms-Forge-${project.ext.fullVersion}.jar"

    from {
        project(':forge').tasks.shadowJar.archiveFile
    }

    dependencies {
        include(dependency('net.luckperms:.*'))
        include(dependency('me.lucko.luckperms:.*'))
    }
}

artifacts {
    archives shadowJar
}
